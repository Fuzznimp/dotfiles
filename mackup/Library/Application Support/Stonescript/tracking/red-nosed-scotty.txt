// RedNosedScotty v1.2

//   Written By: qbodp (Refer code: BBGG)

//   Inspired from: FreeChestTracker v1.1.1
//     Cancerorus Potatoes (Discord: 10100111001)

//   Description:
//     "Skully the Red-Nosed Skull" hid the chest!

//   To Use:
//     Mobile user?
//     Simply "Copy-Paste" script!

//     Importing?
//     Simply "import ...", example:
//       import RedNosedScotty
//     If @rnsMobile@ is turned OFF:
//       var rednose = import RedNosedScotty
//       rednose.RedNose()

//     x-ray mode?
//     Turn ON @rnsxray@ under "Customisation".

//   Depends On:
//     Nothing.

//   What it does:
//     • Gives skull hiding the chest a red nose.
//     • Should work on most screen resolutions.

/*===============
  Changelog
  v1.0  Gives the skull with chest a red nose.
  v1.1  Avoids (all?) dialog disruptions.
  v1.2  Added "Copy-Paste" Easy Activation.
===============*/
/*===============
  ToDo
  • Prevent disturbance from skull switch
    after 1st correct guess.
===============*/

/*===============
  Customisation
===============*/

// "Copy-Paste" easy Active
// "1" to turn ON
// "0" to turn OFF
var rnsMobile = 1

/* skull mouth position, dialog box detection
   and matching indicator */
// "1" to turn ON
// "0" to turn OFF
var rnsxray = 0

// skull nose offsets
// (skull’s mouth must be right under the nose)
var rnsxo = 0 // x offset
var rnsyo = 0 // y offset

/*===================
  End Customisation
===================*/

// dialog box related vars
//// dialog box char
var rnsdh = "─" // speech bubble horizontal bar
var rnsda = "." // speech bubble "arrow"
//// dialog box matching
var rnsdm = 9 // height to check
var rnsdb = 0 // having dialog box?
// skull related vars
//// skully mouth char
var rnsm1 = "." // small skull mouth
var rnsm2 = "." // Scotty's mouth
var rnsm3 = "." // Scotty's Jaw 1
var rnsm4 = "." // Scotty's Jaw 2
//// screen middle pos
var rnsxmid = screen.w/2
var rnsymid = screen.h/2
//// skully mth pos
var rnsxl
var rnsyl
var rnsxm
var rnsym
var rnsxr
var rnsyr
//// skully mth matching
var rnsml
var rnsmlb
var rnsmm
var rnsmmb
var rnsmr
var rnsmrb
// chest related vars
//// chest pos
var posc = 2
//// check if swapped
var poscswapped = 1

// "Copy-Paste" Easy Activation.
?rnsMobile = 1
  RedNose()

func RedNose()
  ?loc="undead_crypt_intro"&pos.x>=52
    // get skully mouth pos
    rnsxl = rnsxmid-4+rnsxo
    rnsyl = rnsymid-9+rnsyo
    rnsxm = rnsxmid+2+rnsxo
    rnsym = rnsymid-6+rnsyo
    rnsxr = rnsxmid+8+rnsxo
    rnsyr = rnsymid-3+rnsyo
    // get skully mouth symbol
    ?rnsm1="."&rnsm2="."&screen.x>=69
      rnsda = draw.GetSymbol(rnsxr+1, rnsyr)
      rnsm1 = draw.GetSymbol(rnsxr, rnsyr)
      rnsm2 = draw.GetSymbol(rnsxm, rnsym)
      rnsm3 = draw.GetSymbol(rnsxm-1, rnsym)
    // get symbol at mouth pos
    rnsml = draw.GetSymbol(rnsxl, rnsyl)
    rnsmm = draw.GetSymbol(rnsxm, rnsym)
    rnsmr = draw.GetSymbol(rnsxr, rnsyr)
    // Check mouth pos is @rnsm1@ / @rnsm2@.
    ?rnsml=rnsm1|rnsml=rnsm2
      rnsmlb = 1
    :
      rnsmlb = 0
    ?rnsmm=rnsm1|rnsmm=rnsm2
      rnsmmb = 1
    :
      rnsmmb = 0
    ?rnsmr=rnsm1|rnsmr=rnsm2
      rnsmrb = 1
    :
      rnsmrb = 0
    // Show mouth present
    ?rnsxray=1
      ?rnsmlb = 1
        draw.Bg(rnsxl, rnsyl, "#00FF00")
      ?rnsmmb = 1
        draw.Bg(rnsxm, rnsym, "#00FF00")
      ?rnsmrb = 1
        draw.Bg(rnsxr, rnsyr, "#00FF00")
    // check for dialog box
    rnsdb = 0 // resetting to "no dialog box"
    ?rnsml=rnsm3|rnsml=rnsm4
      rnsdb = 1
      ?rnsxray=1
        draw.Bg(rnsxl, rnsyl, "#00FF00")
    :?rnsmm=rnsm3|rnsmm=rnsm4
      rnsdb = 1
      ?rnsxray=1
        draw.Bg(rnsxm, rnsym, "#00FF00")
    :?rnsmr=rnsm3|rnsmr=rnsm4
      rnsdb = 1
      ?rnsxray=1
        draw.Bg(rnsxr, rnsyr, "#00FF00")
    :
      for dlgi = 2..rnsdm
        ?rnsml=rnsm2
          ?rnsda=
          ^draw.GetSymbol(rnsxl-1,rnsyl+dlgi)|
          ^rnsdh=
          ^draw.GetSymbol(rnsxl-1,rnsyl+dlgi)
            rnsdb = 1
            ?rnsxray=1
              draw.Bg(rnsxl-1,rnsyl+dlgi,
              ^"#00FF00")
        :?rnsmm=rnsm2
          ?rnsda=
          ^draw.GetSymbol(rnsxm-1,rnsym+dlgi)|
          ^rnsdh=
          ^draw.GetSymbol(rnsxm-1,rnsym+dlgi)
            rnsdb = 1
            ?rnsxray=1
              draw.Bg(rnsxm-1,rnsym+dlgi,
              ^"#00FF00")
        :?rnsmr=rnsm2
          ?rnsda=
          ^draw.GetSymbol(rnsxr-1,rnsyr+dlgi)|
          ^rnsdh=
          ^draw.GetSymbol(rnsxr-1,rnsyr+dlgi)
            rnsdb = 1
            ?rnsxray=1
              draw.Bg(rnsxr-1,rnsyr+dlgi,
              ^"#00FF00")
    // no scotty dialog box onscreen
    ?rnsdb = 0
      // check ready to swap @posc@
      ?rnsmlb=1&rnsmmb=1&rnsmrb=1
        poscswapped = 0
      // get initial @posc@
      :?rnsmlb=0&rnsmmb=1&rnsmrb=1
        posc = 1
      :?rnsmlb=1&rnsmmb=0&rnsmrb=1
        posc = 2
      :?rnsmlb=1&rnsmmb=1&rnsmrb=0
        posc = 3
      // check head swaps &
      // get new @posc@
      :?rnsmlb=1&rnsmmb=0&rnsmrb=0
        ?poscswapped = 0
          ?posc = 2
            posc = 3
          :?posc = 3
            posc = 2
          poscswapped = 1
      :?rnsmlb=0&rnsmmb=0&rnsmrb=1
        ?poscswapped = 0
          ?posc = 1
            posc = 2
          :?posc = 2
            posc = 1
          poscswapped = 1
    // show @posc@
    ?posc = 1
      draw.Bg(rnsxl, rnsyl-1, "#FF0000")
    :?posc = 2
      draw.Bg(rnsxm, rnsym-1, "#FF0000")
    :?posc = 3
      draw.Bg(rnsxr, rnsyr-1, "#FF0000")
